query=PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT (?allarsuri AS ?_id) ('finto' as ?type) (CONCAT(?prefLabelFI, '|', ?prefLabelSV) AS ?prefLabels)
  (CONCAT(GROUP_CONCAT(DISTINCT ?altLabelFI; separator='|'), '|', GROUP_CONCAT(DISTINCT ?altLabelSV; separator='|')) AS ?altLabels)
FROM <http://www.yso.fi/onto/ysa/>
FROM <http://www.yso.fi/onto/allars/>
WHERE {
  ?allarsuri a skos:Concept ; skos:inScheme <http://www.yso.fi/onto/allars/> .
  ?allarsuri skos:prefLabel ?prefLabelSV .
  FILTER(LANG(?prefLabelSV)='sv')
  OPTIONAL {
    ?allarsuri skos:altLabel ?altLabelSV .
    FILTER(LANG(?altLabelSV)='sv')
  }
  OPTIONAL {
    ?allarsuri skos:exactMatch ?ysauri .
    ?ysauri skos:inScheme <http://www.yso.fi/onto/ysa/> .
    ?ysauri skos:prefLabel ?prefLabelFI .
    FILTER(LANG(?prefLabelFI)='fi')
    OPTIONAL {
      ?ysauri skos:altLabel ?altLabelFI .
      FILTER(LANG(?altLabelFI)='fi')
    }
  }
}
GROUP BY ?allarsuri ?prefLabelFI ?prefLabelSV
